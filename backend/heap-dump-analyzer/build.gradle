/********************************************************************************
 * Copyright (c) 2020, 2021 Contributors to the Eclipse Foundation
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0
 *
 * SPDX-License-Identifier: EPL-2.0
 ********************************************************************************/
plugins {
    id 'com.diffplug.p2.asmaven' version '3.27.0'
}

p2AsMaven {
    group 'eclipse-deps', {
        repoEclipse '4.8'
        iu            'com.ibm.icu'
        iu            'org.eclipse.core.commands'
        iu            'org.eclipse.core.contenttype'
        iu            'org.eclipse.core.jobs'
        iu            'org.eclipse.core.runtime'
        iu            'org.eclipse.equinox.app'
        iu            'org.eclipse.equinox.common'
        iu            'org.eclipse.equinox.preferences'
        iu            'org.eclipse.equinox.registry'
        iu            'org.eclipse.osgi'
        append        true
        slicingOption 'includeFeatures', 'false'
        slicingOption 'includeNonGreedy', 'false'
        slicingOption 'includeOptional', 'false'
    }

    group 'eclipse-mat', {
        repo          'http://download.eclipse.org/mat/1.11.0/update-site/'
        iu            'org.eclipse.mat.api'
        iu            'org.eclipse.mat.hprof'
        iu            'org.eclipse.mat.parser'
        iu            'org.eclipse.mat.report'
        append        true
        slicingOption 'includeFeatures', 'false'
        slicingOption 'includeNonGreedy', 'false'
        slicingOption 'includeOptional', 'false'
    }
}

task copyMATJar(type: Copy) {
    from "${rootProject.rootDir}/backend/heap-dump-analyzer/build/p2asmaven/p2/eclipse-deps/plugins"
    from "${rootProject.rootDir}/backend/heap-dump-analyzer/build/p2asmaven/p2/eclipse-mat/plugins"
    from "${rootProject.rootDir}/backend/heap-dump-analyzer/build/p2asmaven/token-eclipse-mat"
    into "${rootProject.rootDir}/mat-deps"
    include '*.jar'
    rename '(.+)_(.+)', '$1.jar'
}

task copyImplJar(type: Copy) {
    from 'impl/build/libs/impl-1.0.jar'
    into "${rootProject.rootDir}/mat-deps"
}

copyImplJar.dependsOn(':backend:heap-dump-analyzer:impl:build')

task prepareMatMaterials(dependsOn: [copyMATJar])
task prepareHeapDumpAnalyzerImpl(dependsOn: [copyImplJar])

prepareHeapDumpAnalyzerImpl.configure {
    shouldRunAfter prepareMatMaterials
}
